plugins {
    //id "java-library"
    id "org.jetbrains.kotlin.jvm" version "1.6.0"
}

wrapper {
    gradleVersion = "7.2"
}

dependencies {
    implementation project(":app")
    implementation project(":core")
}

jar {
    manifest {
        attributes("Main-Class": "com.mower.MowerApp")
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

allprojects {
    group "com.mower"
    version "1.0.0"
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: "kotlin"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    dependencies {
        testImplementation "org.mockito:mockito-junit-jupiter:3.9.0"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
    }

    test {
        useJUnitPlatform()
    }
}
